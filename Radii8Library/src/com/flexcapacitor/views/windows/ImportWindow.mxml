<?xml version="1.0" encoding="utf-8"?>
<w:AnimatedPanelWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   xmlns:c="com.flexcapacitor.views.*"
			   xmlns:handlers="com.flexcapacitor.handlers.*"
			   xmlns:fc="com.flexcapacitor.effects.popup.*"
			   xmlns:w="com.flexcapacitor.views.windows.*"

			   title="Import MXML"
			   title.revisions="Select Revision"
			   creationComplete="animatedpanelwindow1_creationCompleteHandler(event)"
			   >
	
	<fx:Script>
		<![CDATA[
			import com.flexcapacitor.controller.Radiate;
			import com.flexcapacitor.model.DocumentRevision;
			import com.flexcapacitor.model.IDocument;
			import com.flexcapacitor.model.IProject;
			import com.flexcapacitor.model.SourceData;
			import com.flexcapacitor.utils.AceEditorUtils;
			import com.flexcapacitor.utils.XMLUtils;
			import com.flexcapacitor.utils.supportClasses.XMLValidationInfo;
			
			import mx.collections.ArrayCollection;
			import mx.core.IVisualElement;
			import mx.core.UIComponent;
			import mx.events.FlexEvent;
			
			public static const NEW_DOCUMENT:String = "newDocument";
			public static const CURRENT_DOCUMENT:String = "currentDocument";
			public static const CURRENT_SELECTION:String = "currentSelection";
			public static const IMPORT:String = "import";
			
			public static const NORMAL:String = "normal";
			public static const REVISIONS:String = "revisions";
			
			private var _code:String;

			public function get code():String {
				var text:String;
				
				if (aceEditor) {
					text = aceEditor.text;
				}
				else {
					return _code;
				}
				return text;
			}

			public function set code(value:String):void {
				_code = value;
				if (aceEditor) {
					aceEditor.text = value;
				}
			}

			public var warningMessage:String = "Warning: XML is invalid and may not import correctly.";
			
			public var aceEditor:*;
			public var aceCSSEditor:*;
			public var templateEditor:*;
			
			public var editorMargin:String = "4px 0 8px 0px";
			public var aceEditorCreated:Boolean;
			
			public var showRevisions:Boolean = false;
			
			private function createEditors():void {
				// 1067: Implicit coercion of a value of type com.flexcapacitor.controls:AceEditor to an unrelated type Object
				
				aceEditor = AceEditorUtils.createInstance();
				UIComponent(aceEditor).percentWidth = 100;
				UIComponent(aceEditor).percentHeight = 100;
				aceEditor.top = 0;
				aceEditor.left = 0;
				editorGroup.addElementAt(aceEditor, 1);
				aceEditor.mode = "ace/mode/xml";
				aceEditor.showFoldWidgets = true;
				aceEditor.margin = editorMargin;
				aceEditor.useWordWrap = true;
				aceEditor.addEventListener(FocusEvent.FOCUS_IN, editorFocusInHandler, false, 0, true);
				aceEditor.addEventListener(Event.CHANGE, changeHandler, false, 0, true);
				aceEditorCreated = true;
				aceEditor.scrollSpeed = .5;
				
			}
			
			private var lastFocusedEditor:Object;
			
			private function editorFocusInHandler(event:FocusEvent):void {
				lastFocusedEditor = event.currentTarget;
			}
			
			protected function changeHandler(event:Event = null):void {
				var validationInfo:XMLValidationInfo;
				var errorLocation:String;
				var isValid:Boolean;
				var message:String;
				var error:Object;
				
				isValid = XMLUtils.isValidXML(code);
				//error = XMLUtils.validationError;
				
				if (!isValid && code!="") {
					validationInfo = XMLUtils.validateXML(code);
					message = XMLUtils.validationError ? "" +  validationInfo.internalErrorMessage : warningMessage;
					errorLocation = "At line " + (validationInfo.row) + " column " + validationInfo.column+ ":";
					showMessage(errorLocation, message, validationInfo.row-1, validationInfo.column);
				}
				else {
					hideMessage();
				}
				
			}
			
			public function showMessage(firstMessage:String, secondMessage:String = null, row:int = -1, column:int = 0):void {
				lineError.text = firstMessage;
				warningLabel.text = secondMessage;
				errorGroup.visible = true;
				errorGroup.includeInLayout = true;
				aceEditor.clearAnnotations();
				aceEditor.setAnnotation(row, column, secondMessage, "error");
			}
			
			public function hideMessage():void {
				errorGroup.visible = false;
				errorGroup.includeInLayout = false;
				lineError.text = "";
				warningLabel.text = "";
				
				if (aceEditor.aceFound) {
					aceEditor.clearAnnotations();
				}
			}
			
			public function showImportedMessage():void {
				hideMessage();
				
				notesGroup.visible = false;
				notesGroup.includeInLayout = false;
				messageLabel.text = "Import successful";
				
				if (aceEditor.aceFound) {
					aceEditor.clearAnnotations();
				}
			}
			
			/**
			 * Import 
			 * */
			public function importPopUp_closeHandler(event:Event):void {
				var radiate:Radiate = Radiate.getInstance();
				var selectedDocument:IDocument = radiate.selectedDocument;
				var selectedProject:IProject = radiate.selectedProject;
				var type:String = importLocation.selectedValue as String;
				var target:IVisualElement = radiate.target as IVisualElement;
				var result:SourceData;
				
				if (type==ImportWindow.NEW_DOCUMENT) {
					result = radiate.importMXMLDocument(selectedProject, null, null, code);
					
					if (result) {
						showImportedMessage();
						return;
					}
				}
				else if (type==ImportWindow.CURRENT_DOCUMENT && selectedDocument) {
					result = radiate.importMXMLDocument(selectedProject, selectedDocument, null, code);
					
					if (result) {
						showImportedMessage();
						return;
					}
				}
				else if (type==ImportWindow.CURRENT_SELECTION) {
					if (target is IVisualElement) {
						result = radiate.importMXMLDocument(selectedProject, selectedDocument, target, code);
						
						if (result) {
							showImportedMessage();
							return;
						}
					}
					else {
						showMessage("Please select a visual element");
						return;
					}
				}
				else {
					showMessage("Please select a document");
					return;
				}
				
				if (result) {
					showImportedMessage();
				}
				else {
					showMessage("Did not import successfully");
				}
				
			}
			
			protected function animatedpanelwindow1_hideHandler(event:Event):void {
				radiate = null;
				code = null;
				if (revisionsCollection) revisionsCollection.removeAll();
				documentRevisions = null;
			}
			
			protected function animatedpanelwindow1_creationCompleteHandler(event:FlexEvent):void {
				radiate = Radiate.instance;
				var selectedDocument:IDocument = radiate.selectedDocument;
				
				Radiate.callAfter(200, afterCompleteTasks);
				
				if (showRevisions) {
					currentState = REVISIONS;
					validateNow();
					documentRevisions = selectedDocument.revisions.slice();
					revisionsCollection = new ArrayCollection(documentRevisions);
					revisionsList.dataProvider = revisionsCollection;
				}
				else {
					currentState = NORMAL;
				}
				
			}
			
			public var documentRevisions:Array;
			public var revisionsCollection:ArrayCollection;
			
			public function afterCompleteTasks():void {
				createEditors();
				
				if (_code && aceEditor) {
					if (aceEditor.text!=_code) {
						aceEditor.text = _code;
						changeHandler();
					}
				}
			}
			
			protected function revisions_clickHandler(event:MouseEvent):void
			{
				var selectedItem:DocumentRevision = revisionsList.selectedItem as DocumentRevision;
				
				if (selectedItem) {
					code = selectedItem.code;
				}
			}
			
			protected function showRevisionsButton_clickHandler(event:MouseEvent):void {
				var selectedDocument:IDocument = radiate.selectedDocument;
				
				if (currentState == REVISIONS) {
					currentState = NORMAL;
				}
				else {
					currentState = REVISIONS;
					validateNow();
					
					if (selectedDocument) {
						documentRevisions = selectedDocument.revisions.slice();
						revisionsCollection = new ArrayCollection(documentRevisions);
						revisionsList.dataProvider = revisionsCollection;
					}
				}
			}
			
			protected function showOriginalSourceButton_clickHandler(event:MouseEvent):void {
				var selectedDocument:IDocument = radiate.selectedDocument;
				
				if (selectedDocument) {
					if (selectedDocument.originalSource) {
						aceEditor.text = selectedDocument.originalSource;
					}
					else {
						Radiate.warn("No source in document.");
					}
				}
				else {
					Radiate.warn("No document is selected.");
				}
			}
			
			protected function showCurrentSourceButton_clickHandler(event:MouseEvent):void {
				var selectedDocument:IDocument = radiate.selectedDocument;
				
				if (selectedDocument) {
					var source:String =  selectedDocument.getSource();
					aceEditor.text = source;
				}
				else {
					Radiate.warn("No source to show. No document is selected.");
				}
			}
			
			protected function cancelButton_clickHandler(event:MouseEvent):void{
				action = null;
				code = null;
				
				closePopUp.startDelay = 500;
				closePopUp.play();
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		
		
		<!--- hide about popup -->
		<fc:ClosePopUp id="closePopUp" popUp="{this}"  />
		
		<fx:String id="action"></fx:String>
		
		<s:RadioButtonGroup id="importLocation"  />
	</fx:Declarations>
	
	<w:states>
		<s:State name="normal"/>
		<s:State name="revisions"/>
	</w:states>
	
	<mx:HDividedBox left="15" 
			  top="20" 
			  right="20"
			  bottom="20">
		
		<s:VGroup height="100%" includeIn="revisions">
			<s:Label x="15" y="65" text="Revisions:" fontWeight="bold"/>
			<s:List id="revisionsList" 
					width="100%"
					height="100%" 
					minWidth="200"
					labelField="name" 
					borderAlpha=".5"
					click="revisions_clickHandler(event)"/>
		</s:VGroup>
		
		<s:VGroup id="editorGroup" 
				  left="15" 
				  top="20" 
				  right="20"
				  bottom="20"
				  width="100%"
				  height="100%">
			
			<s:Label x="15" y="65" text="MXML code:" fontWeight="bold"/>
			
			<s:HGroup x="14" y="31" width="100%" height="22" 
					  horizontalAlign="right" 
					  verticalAlign="middle">
				<s:Label x="15" y="15" text="Import to:" 
						 fontWeight="bold" paddingTop="3" paddingRight="10"/>
				<s:RadioButton x="91" 
							   y="128" 
							   label="New Document" 
							   selected="false" 
							   group="{importLocation}"
							   value="{NEW_DOCUMENT}"
							   />
				<s:RadioButton x="190" 
							   y="130" 
							   selected="true"
							   label="Current Document" 
							   group="{importLocation}"
							   value="{CURRENT_DOCUMENT}"
							   />
				<s:RadioButton x="190" 
							   y="130" 
							   label="Selected Component" 
							   group="{importLocation}"
							   value="{CURRENT_SELECTION}"
							   />
				<s:Spacer width="100%"/>
			</s:HGroup>
			
			<s:HGroup id="notesGroup" width="100%" verticalAlign="middle" 
					  includeInLayout="false" visible="false">
				<s:Label id="messageLabel"
						 fontWeight="bold" 
						 color="green"/>
				
			</s:HGroup>
			
			<s:HGroup id="errorGroup" width="100%" verticalAlign="middle" 
					  includeInLayout="false" visible="false">
				<s:Label id="lineError" 
						 x="15" y="15" 
						 text="" 
						 fontWeight="bold" 
						 color="red"/>
				
				<s:Label id="warningLabel" 
						 x="15" y="15" 
						 text="" 
						 fontWeight="bold" 
						 color="red"
						 width="100%"/>
			</s:HGroup>
			
			<s:HGroup width="100%" right="10" bottom="10" verticalAlign="baseline">
				<s:Button id="showRevisionsButton" label.normal="Show Revisions"
						  label.revisions="Hide Revisions"
						  click="showRevisionsButton_clickHandler(event)"/>
				<s:Button id="showOriginalSourceButton" 
						  label="Show Original Source"
						  click="showOriginalSourceButton_clickHandler(event)"/>
				<s:Button id="showCurrentSourceButton" 
						  label="Show Current Source"
						  click="showCurrentSourceButton_clickHandler(event)"/>
				<s:Spacer width="100%"/>
				<s:Button id="cancelButton" label="Close" click="cancelButton_clickHandler(event)"/>
				<s:Button id="importButton" label="Import" click="importPopUp_closeHandler(event)"/>
			</s:HGroup>
		</s:VGroup>
	</mx:HDividedBox>
	
</w:AnimatedPanelWindow>
