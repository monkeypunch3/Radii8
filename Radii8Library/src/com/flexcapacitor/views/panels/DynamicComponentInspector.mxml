<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 xmlns:flexcapacitor="flexcapacitor.*"
		 
		 width="100%" height="100%"
		 fontSize="11"
		 implements="com.flexcapacitor.views.IInspector"
		 >
	
	<fx:Script>
		<![CDATA[
			import com.flexcapacitor.controller.Radiate;
			import com.flexcapacitor.events.RadiateEvent;
			import com.flexcapacitor.managers.InspectorManager;
			import com.flexcapacitor.model.InspectorData;
			import com.flexcapacitor.performance.PerformanceMeter;
			import com.flexcapacitor.utils.supportClasses.ComponentDescription;
			import com.flexcapacitor.views.IInspector;
			
			import mx.core.IVisualElement;
			import mx.core.IVisualElementContainer;
			import mx.core.UIComponent;
			
			/**
			 * Reference to Radiate
			 * */
			public var radiate:Radiate;
			
			public var componentDescription:ComponentDescription;

			private var _target:Object;

			public function get target():Object
			{
				return _target;
			}

			public function set target(value:Object):void
			{
				_target = value;
			}

			public var subTarget:Object;
			
			public var property:String;
			public var propertyIndex:int;
			
			public var removeInspectors:Boolean = false;
			
			private var _inspectors:Array;
			
			public function get inspectors():Array {
				return _inspectors;
			}
			
			[Bindable]
			public function set inspectors(value:Array):void {
				
				_inspectors = value;
				
				if (_inspectors) {
					
				}
				else {
					
				}
			}
			
			public function activate(selectedTarget:Object = null):void {
				radiate = Radiate.instance;
				
				// listen at a higher priority so we can deactivate before child inspectors hear event 
				// before they have to since the views are specific to the class they are used for
				// for example, if we have inspectors for Button and we switch to Application
				// we don't want the inspectors for Button to get the target change event 
				// for Application
				radiate.addEventListener(RadiateEvent.TARGET_CHANGE, handleTargetChange, false, 10, true);
				//radiate.addEventListener(RadiateEvent.PROPERTY_CHANGED, propertyChangeHandler, false, 0, true);
				
				if (radiate.target) {
					target = radiate.target;
				}
				if (radiate.subTarget) {
					target = radiate.subTarget;
				}
				
				inspectors = InspectorManager.getInspectors(target);
				
				//PerformanceMeter.start("Updating inspectors");
				if (removeInspectors) {
					updateInspectors2(inspectors, target);
				}
				else {
					updateInspectors(inspectors, target);// 498,527,414
				}
				//PerformanceMeter.stop("Updating inspectors", true);
			}
			
			public function deactivate():void {
				if (radiate) {
					radiate.removeEventListener(RadiateEvent.TARGET_CHANGE, handleTargetChange);
				}
			}
			
			protected function handleTargetChange(event:RadiateEvent):void {
				//var sameType:Boolean = radiate.isSameClassType(target, radiate.target);
				
				if (event.subSelectedItem) {
					target = event.subSelectedItem;
				}
				else if (target!=event.selectedItem || property!=event.property || propertyIndex!=event.propertyIndex) {
					target = event.selectedItem;
					property = event.property;
					propertyIndex = event.propertyIndex;
				}
				else {
					return;
				}
				
				inspectors = InspectorManager.getInspectors(target);
				
				//PerformanceMeter.start("Updating inspectors");
				if (removeInspectors) {
					updateInspectors2(inspectors, target, property, propertyIndex);
				}
				else {
					updateInspectors(inspectors, target, property, propertyIndex);
				}
				//PerformanceMeter.stop("Updating inspectors", true);
			}
			
			/**
			 * Gets a group of inspectors and shows it, hiding old inspectors
			 *           app, button, combobox 
			 * First run 481ms, 273ms, 242ms
			 * Second run 190ms, 129ms, 130ms
			 **/
			public function updateInspectors(inspectors:Array, selectedTarget:Object = null, property:String = null, index:int = -1):void {
				var inspectorInstance:UIComponent;
				var inspectorData:InspectorData;
				var numberOfDynamicInspectors:int;
				var targetInspectorGroup:IVisualElementContainer;
				var inspector:IInspector;
				var currentInspectorGroup:IVisualElement;
				var numberOfInspectors:int = contentGroup.numElements;
				var removeGroups:Boolean;
				
				if (contentGroup.numElements) {
					
					if (removeGroups) {
						currentInspectorGroup = contentGroup.getElementAt(0);
					}
					
					for (var k:int = 0; k < numberOfInspectors; k++)  {
						currentInspectorGroup = contentGroup.getElementAt(k);
						if (currentInspectorGroup.visible) {
							break;
						}
					}
					
					numberOfDynamicInspectors = IVisualElementContainer(currentInspectorGroup).numElements;
				}
				
				// deactivate previous inspectors 0ms
				for (var j:int;j<numberOfDynamicInspectors;j++) {
					inspector = IVisualElementContainer(currentInspectorGroup).getElementAt(j) as IInspector;
					
					// if inspector components have focus out handlers they may 
					// set a value to the new component instead of the old one
					if (inspector) {
						inspector.deactivate();
					}
				}
				
				targetInspectorGroup = InspectorManager.getInspectorContainer(selectedTarget);
				
				// if not the same type we need to get the type we need
				if (targetInspectorGroup!=currentInspectorGroup) {
					
					if (currentInspectorGroup) {
						// remove or hide previous inspector group 0ms
						if (removeGroups) {
							contentGroup.removeElement(currentInspectorGroup);
						}
						else {
							currentInspectorGroup.visible = false;
							currentInspectorGroup.includeInLayout = false;
						}
					}
				}
				
				// adding target inspector group 40ms-100ms
				if (targetInspectorGroup) {
					if (!contentGroup.containsElement(targetInspectorGroup as IVisualElement)) {
						contentGroup.addElement(targetInspectorGroup as IVisualElement);
					}
				}
				
				numberOfDynamicInspectors = targetInspectorGroup.numElements;
				
				// activate inspectors
				//PerformanceMeter.start("Activating inspectors");
				for (var i:int;i<numberOfDynamicInspectors;i++) {
					inspectorInstance = targetInspectorGroup.getElementAt(i) as UIComponent;
					
					if (inspectorInstance) {
						//PerformanceMeter.start("Activating inspector");
						inspectorInstance.includeInLayout = false;
						IInspector(inspectorInstance).activate(selectedTarget);
						inspectorInstance.includeInLayout = true;
						//PerformanceMeter.stop("Activating inspector", true);
						//trace(" " + ClassUtils.getClassName(inspectorInstance));
					}
				}
				//PerformanceMeter.stop("Activating inspectors", true);
				
				// show target inspector group
				IVisualElement(targetInspectorGroup).visible = true;
				IVisualElement(targetInspectorGroup).includeInLayout = true;
				
				if (numberOfDynamicInspectors==0) {
					noInspectorsLabel.visible = true;
					noInspectorsLabel.includeInLayout = true;
				}
				else {
					noInspectorsLabel.visible = false;
					noInspectorsLabel.includeInLayout = false;
				}
				
			}
			
			/**
			 * Removes old inspectors and adds target specific ones
			 *           app, button, combobox 
			 * First run 768ms, 273ms, 443ms
			 * Second run 190ms, 129ms, 130ms
			 **/
			public function updateInspectors2(inspectors:Array, selectedTarget:Object = null, property:String = null, index:int = -1):void {
				var inspectorInstance:UIComponent;
				var inspectorData:InspectorData;
				var numberOfDynamicInspectors:int = contentGroup.numElements;
				
				// deactivate and remove previous inspectors
				for (var j:int;j<numberOfDynamicInspectors;j++) {
					if (contentGroup.getElementAt(j) is IInspector) {
						IInspector(contentGroup.getElementAt(j)).deactivate();
					}
				}
				
				contentGroup.removeAllElements();
				
				numberOfDynamicInspectors = inspectors.length;
				
				// add and activate inspectors
				for (var i:int;i<numberOfDynamicInspectors;i++) {
					inspectorData = InspectorData(inspectors[i]);
					inspectorInstance = inspectorData.getInstance() as UIComponent;
					
					if (inspectorInstance) {
						contentGroup.addElement(inspectorInstance);
						//IInspector(inspectorInstance).activate(selectedTarget, property, propertyIndex);
						IInspector(inspectorInstance).activate(selectedTarget);
					}
				}
				
				if (numberOfDynamicInspectors==0) {
					noInspectorsLabel.visible = true;
					noInspectorsLabel.includeInLayout = true;
				}
				else {
					noInspectorsLabel.visible = false;
					noInspectorsLabel.includeInLayout = false;
				}
				
			}
		]]>
	</fx:Script>
	
	
	<s:Scroller id="scroller" top="0" left="8" right="8" bottom="4">
		<s:Group id="contentGroup" width="100%" right="12" bottom="12" />
	</s:Scroller>
	
	
	<s:Label id="noInspectorsLabel" 
			 verticalCenter="0" 
			 textAlign="center"
			 width="100%"
			 paddingLeft="10" paddingRight="10"
			 color="#666666"
			 text="The property inspector is not available for this component yet. Please switch to the all properties view."
			 />
</s:Group>