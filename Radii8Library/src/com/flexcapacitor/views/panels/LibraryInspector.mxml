<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 xmlns:c="com.flexcapacitor.controls.*"
		 xmlns:handlers="com.flexcapacitor.handlers.*" 
		 xmlns:collections="com.flexcapacitor.effects.collections.*"
		 xmlns:status="com.flexcapacitor.effects.status.*"
		 xmlns:clipboard="com.flexcapacitor.effects.clipboard.*"
		 xmlns:file="com.flexcapacitor.effects.file.*"
		 xmlns:fc="com.flexcapacitor.effects.popup.*"
		 xmlns:inspectors="com.flexcapacitor.views.inspectors.*"
		 xmlns:supportClasses="com.flexcapacitor.effects.collections.supportClasses.*" 
		 
		 implements="com.flexcapacitor.views.IInspector" 
		 width="400" 
		 height="100%" 
		 paddingLeft="8" 
		 paddingRight="8" 
		 paddingBottom="8" 
		 
		 dragEnter="library_dragEnterHandler(event)"
		 dragDrop="library_dragDropHandler(event)"
		 dragOver="library_dragOverHandler(event)"
		 >
	
	<!-- 
	
	The list of components to include come from spark-manifest-defaults.xml 
	in Radii8LibrarySparkAssets. The include attribute must be set to true for them to be visible. 
	
	The component classes are included by creating a reference to them in Radii8LibrarySparkAssets.
	
	We create an instance of the class in the mouse down handler. 
	
	
	-->
	
	
	<fx:Script>
		<![CDATA[
			import com.flexcapacitor.controller.Radiate;
			import com.flexcapacitor.events.DragDropEvent;
			import com.flexcapacitor.events.RadiateEvent;
			import com.flexcapacitor.model.AttachmentData;
			import com.flexcapacitor.model.DocumentData;
			import com.flexcapacitor.model.IDocument;
			import com.flexcapacitor.model.ImageData;
			import com.flexcapacitor.services.WPAttachmentService;
			import com.flexcapacitor.utils.ClassUtils;
			import com.flexcapacitor.utils.DisplayObjectUtils;
			import com.flexcapacitor.utils.DragManagerUtil;
			import com.flexcapacitor.utils.supportClasses.ComponentDefinition;
			import com.flexcapacitor.views.IInspector;
			import com.flexcapacitor.views.ImageView;
			import com.flexcapacitor.views.windows.DeleteDocumentWindow;
			
			import mx.core.ClassFactory;
			import mx.core.DragSource;
			import mx.core.IUIComponent;
			import mx.core.UIComponent;
			import mx.events.DragEvent;
			import mx.events.SandboxMouseEvent;
			import mx.managers.DragManager;
			
			import spark.components.Application;
			import spark.components.Group;
			import spark.components.Image;
			import spark.components.Label;
			import spark.events.IndexChangeEvent;
			
			[Bindable]
			private var radiate:Radiate;
			
			private var target:Object;
			
			private var dragManagerInstance:DragManagerUtil;
			
			public var selectedItem:Object;
			
			public var SMALL:String = "small";
			public var MEDIUM:String = "medium";
			public var FULL:String = "full";
			public var THUMBNAIL:String = "thumbnail";
			
			public var service:WPAttachmentService;
			
			public var currentTargetRow:Group;
			public var fileReference:FileReference;
			public var fileReferenceList:FileReferenceList;
			
			public var uploadAutomatically:Boolean;
			
			public function activate():void {
				radiate = Radiate.getInstance();
				
				radiate.addEventListener(RadiateEvent.ATTACHMENT_UPLOADED, uploadAttachmentResult, false, 0, true);
				radiate.addEventListener(RadiateEvent.ATTACHMENTS_RECEIVED, attachmentsReceived, false, 0, true);
				radiate.addEventListener(RadiateEvent.ASSET_ADDED, assetAdded, false, 0, true);
				radiate.addEventListener(RadiateEvent.ASSET_REMOVED, assetRemoved, false, 0, true);
				radiate.addEventListener(RadiateEvent.DOCUMENT_CHANGE, documentChanged, false, 0, true);
				
				updateAssets();
			}
			
			public function deactivate():void {
				if (radiate) {
					radiate.removeEventListener(RadiateEvent.ATTACHMENT_UPLOADED, uploadAttachmentResult);
					radiate.removeEventListener(RadiateEvent.ATTACHMENTS_RECEIVED, attachmentsReceived);
					radiate.removeEventListener(RadiateEvent.ASSET_ADDED, assetAdded);
					radiate.removeEventListener(RadiateEvent.ASSET_REMOVED, assetRemoved);
					radiate.removeEventListener(RadiateEvent.DOCUMENT_CHANGE, documentChanged);
				}
			}
			
			protected function targetChangeHandler(event:RadiateEvent):void
			{
				target = event.selectedItem;
			}
			
			/**
			 * Listen for drag movement and start dragging if drag tolerance is met
			 * */
			public function rowGroupMouseDownHandler(event:MouseEvent, data:Object, itemIndex:int):void {
				var classFactory:ClassFactory;
				var item:ComponentDefinition;
				var application:Application;
				var component:Object;
				var iDocument:IDocument = radiate.selectedDocument;
				var rowGroup:Group;
				
				selectedItem = data;
				
				item = Radiate.getComponentType("Image");
				
				//dropTargetLabel.text = "";
				
				rowGroup = event.currentTarget as Group;
				
				if (currentTargetRow) {
					removeMouseTargetListeners(currentTargetRow);
				}
				
				//event.stopImmediatePropagation();
				//list.dragEnabled = false;
				
				application = iDocument && iDocument.instance ? iDocument.instance as Application : null;
				
				if (!application) return;
				
				//trace("listening for drag behavior");
				component = Radiate.createComponentToAdd(iDocument, item, false);
				
				
				if (!dragManagerInstance) {
					dragManagerInstance = DragManagerUtil.getInstance();
				}
				
				addMouseTargetListeners(rowGroup);
				
				dragManagerInstance.listenForDragBehavior(rowGroup, iDocument, event, component);
				
				removeDragManagerListeners(dragManagerInstance);
				addDragManagerListeners(dragManagerInstance);
				
				// DRAG DROP 
				// is continued in 
				// DragManagerUtil.dragDropHandler()
				// and then in handleDragDrop() here
			}
			
			/**
			 * Add listeners to drag manager
			 * */
			public function addDragManagerListeners(dragManager:IEventDispatcher):void {
				
				dragManager.addEventListener(DragDropEvent.DRAG_DROP, handleDragDrop);
				dragManager.addEventListener(DragEvent.DRAG_OVER, handleDragOver);
				dragManager.addEventListener(DragDropEvent.DRAG_DROP_COMPLETE, handleDragDropComplete);
				dragManager.addEventListener(DragDropEvent.DRAG_DROP_INCOMPLETE, handleDragDropIncomplete);
			}
			
			/**
			 * Remove listeners from drag manager
			 * */
			public function removeDragManagerListeners(dragManager:IEventDispatcher):void {
				
				dragManager.removeEventListener(DragDropEvent.DRAG_DROP, handleDragDrop);
				dragManager.removeEventListener(DragEvent.DRAG_OVER, handleDragOver);
				dragManager.removeEventListener(DragDropEvent.DRAG_DROP_COMPLETE, handleDragDropComplete);
				dragManager.removeEventListener(DragDropEvent.DRAG_DROP_INCOMPLETE, handleDragDropIncomplete);
				
			}
			
			/**
			 * Listen for mouse events
			 * */
			public function addMouseTargetListeners(rowGroup:IEventDispatcher):void {
				rowGroup.addEventListener(MouseEvent.MOUSE_UP, mouseUpHandler, false, 0, true);
				rowGroup.addEventListener(SandboxMouseEvent.MOUSE_UP_SOMEWHERE, mouseUpHandler, false, 0, true);
				
				stage.addEventListener(MouseEvent.MOUSE_UP, mouseUpHandler, false, 0, true);
				stage.addEventListener(SandboxMouseEvent.MOUSE_UP_SOMEWHERE, mouseUpHandler, false, 0, true);
			}
			
			
			/**
			 * Listen for mouse events
			 * */
			public function removeMouseTargetListeners(eventDispatcher:IEventDispatcher):void {
				if (eventDispatcher) {
					eventDispatcher.removeEventListener(MouseEvent.MOUSE_UP, mouseUpHandler);
					eventDispatcher.removeEventListener(SandboxMouseEvent.MOUSE_UP_SOMEWHERE, mouseUpHandler);
				}
				
				stage.removeEventListener(MouseEvent.MOUSE_UP, mouseUpHandler);
				stage.removeEventListener(SandboxMouseEvent.MOUSE_UP_SOMEWHERE, mouseUpHandler);
			}
			
			/**
			 * Remove mouse handlers from row
			 * */
			protected function mouseUpHandler(event:Event):void {
				removeMouseTargetListeners(currentTargetRow);
				removeMouseTargetListeners(event.currentTarget as IEventDispatcher);
				removeMouseTargetListeners(event.target as IEventDispatcher);
				
				//trace(ClassUtils.getClassName(event.currentTarget) + ":" + event.type);
				
				removeDragManagerListeners(dragManagerInstance);
			}
			
			/**
			 * Handles drag drop event. This is only the first part of adding to the stage. 
			 * We can prevent automatic adding by calling event.preventDefault().
			 * If we do not call preventDefault dragDropComplete is the next event to be called. 
			 * */
			protected function handleDragDrop(event:DragDropEvent):void {
				var component:Object = event.draggedItem;
				//trace(event.type);
				
				dragManagerInstance.removeEventListener(DragDropEvent.DRAG_DROP, handleDragDrop);
				//dropTargetLabel.text = "Drag drop to:" + dragManagerInstance.dropTargetName;
				
				dragManagerInstance.removeEventListener(DragEvent.DRAG_OVER, handleDragOver);
			}
			
			/**
			 * Dispatched after drag drop event. Drag drop can be canceled. If it
			 * is not canceled this event happens. 
			 * */
			protected function handleDragDropComplete(event:DragDropEvent):void {
				var component:Object = event.draggedItem;
				var image:Image = component as Image;
				var attachmentData:AttachmentData = selectedItem as AttachmentData;
				var imageData:ImageData = attachmentData as ImageData;
				var path:String;
				
				//trace(event.type);
				Radiate.updateComponentAfterAdd(radiate.selectedDocument, image, true);
				
				if (image) {
					//image.source = event.draggedItem;
					
					if (imageData is ImageData) {
						path = imageData.url;
						
						if (path) {
							image.width = undefined;
							image.height = undefined;
							//image.source = null;//force it to show up in change history
							Radiate.setProperty(image, "source", path);
						}
						else if (imageData.bitmapData) {
							Radiate.setProperty(image, "source", imageData.bitmapData);
						}
					}
					else {
						image.source = "";
					}
					
				}
				
				removeDragManagerListeners(dragManagerInstance);
			}
			
			protected function handleDragOver(event:Event):void {
				
				//Radiate.info("target: " + dragManagerInstance.lastTargetCandidate);
				//dropTargetLabel.text = "Drag over: " + dragManagerInstance.dropTargetName;
			}
			
			protected function attachmentsReceived(event:RadiateEvent):void {
				var attachments:Array = event.targets as Array;
				radiate.addAssetsToDocument(attachments, radiate.selectedDocument as DocumentData);
				//libraryCollection.source = attachments;
			}
			
			
			protected function smallImage_clickHandler(event:MouseEvent):void {
				var selectedItem:AttachmentData = list.selectedItem as AttachmentData;
				var imageData:ImageData = selectedItem as ImageData;
				
				if (imageData) {
					imageURLInput.text = imageData.smallURL;
				}
			}
			
			protected function thumbnailImage_clickHandler(event:MouseEvent):void {
				setTextInputImageURL(THUMBNAIL);
			}
			
			protected function fullImage_clickHandler(event:MouseEvent):void {
				setTextInputImageURL(FULL);
			}

			protected function mediumImage_clickHandler(event:MouseEvent):void {
				
				setTextInputImageURL(MEDIUM);
			}
			
			/**
			 * Set the URL 
			 * */
			public function setTextInputImageURL(value:String):void {
				var selectedItem:AttachmentData = list.selectedItem as AttachmentData;
				var imageData:ImageData = selectedItem as ImageData;
				
				if (imageData) {
					
					if (value==SMALL) {
						imageURLInput.text = imageData.smallURL;
					}
					else if (value==MEDIUM) {
						imageURLInput.text = imageData.mediumURL;
					}
					else if (value==FULL) {
						imageURLInput.text = imageData.url;
					}
					else if (value==THUMBNAIL) {
						imageURLInput.text = imageData.thumbnailURL;
					}
					
					//imageURLInput.selectRange(imageURLInput.text.length, imageURLInput.text.length);
					imageURLInput.appendText("");
				}
			}
			
			protected function list_changeHandler(event:IndexChangeEvent):void {
				var selectedItem:AttachmentData = list.selectedItem as AttachmentData;
				var imageData:ImageData = selectedItem as ImageData;
				
				if (imageData) {
					setTextInputImageURL(FULL);
				}
				
				if (selectedItem) {
					radiate.dispatchObjectSelectedEvent(selectedItem);
				}
				
				
				if (imageData && openPopUp.isOpen) {
					if (imageData.bitmapData) {
						ImageView(openPopUp.popUp).data = imageData.bitmapData;
					}
					else if (imageData.url) {
						ImageView(openPopUp.popUp).data = imageData.url;
					}
				}
			}
			
			protected function openIcon_clickHandler(event:MouseEvent):void {
				selectFileToUpload();
			}
			
			/**
			 * Select a file for upload
			 * */
			public function selectFileToUpload():void {
				fileReference = new FileReference();
				fileReference.browse();
				fileReference.addEventListener(Event.SELECT, fileSelected, false, 0, true);
				fileReference.addEventListener(Event.COMPLETE, fileComplete, false, 0, true);
			}
			
			/**
			 * File selected
			 * */
			protected function fileSelected(event:Event):void {
				var byteArray:ByteArray = fileReference.data;
				
				fileReference.load();
			}
			
			/**
			 * File load complete
			 * */
			protected function fileComplete(event:Event):void {
				var data:ImageData = new ImageData();
				data.bitmapData;
				
			}
			
			/**
			 * File load complete
			 * */
			protected function loadFile_completeHandler(event:Event):void {
				var imageData:ImageData = new ImageData();
				imageData.bitmapData = loadFile.bitmapData;
				imageData.byteArray = loadFile.data;
				imageData.name = loadFile.currentFileReference.name;
				imageData.contentType = loadFile.loaderContentType;
				imageData.file = loadFile.currentFileReference;
				
				radiate.addAssetToDocument(imageData, radiate.selectedDocument);
				
				list.selectedItem = imageData;
				
				if (uploadAutomatically) {
					uploadAttachment(loadFile.fileReference);
				}
			}
			
			/**
			 * Upload file
			 * */
			public function uploadFile(data:DocumentData):void {
				
				if (false && data && data.file) {
					uploadAttachment(data.file);
				}
				else if (data) {
					if (data.id==null) {
						uploadAttachment(data);
					}
					else {
						Radiate.warn("This item is already uploaded.");
					}
				}
				else {
					
				}
			}
			
			public var currentUpload:AttachmentData;
			
			/**
			 * Create WP service and upload file
			 * */
			public function uploadAttachment(data:Object):void {
				var postID:String;
				var imageData:ImageData = data as ImageData;
				var fileReference:FileReference = data as FileReference;
				
				radiate.addEventListener(RadiateEvent.ATTACHMENT_UPLOADED, uploadAttachmentResult, false, 0, true);

				// upload attachment to project
				postID = radiate.selectedProject ? radiate.selectedProject.id : null;
				
				// if project is not available for some reason upload to document
				if (postID==null) {
					postID = radiate.selectedDocument ? radiate.selectedDocument.id : null;
				}
				
				if (postID==null) {
					Radiate.info("Please save the document or project first.");
				}
				
				if (fileReference) {
					radiate.uploadAttachment(fileReference, postID);
				}
				else if (imageData) {
					currentUpload = imageData;
					radiate.uploadAttachment(imageData.byteArray, postID, imageData.name, null, imageData.contentType); // try to upload to an account not document
				}
			}
			
			protected function uploadAttachmentResult(event:RadiateEvent):void {
				var attachments:Array = event.data as Array;
				var data:Object = event.selectedItem;
				var attachment:AttachmentData;
				var containsName:Boolean;
				var length:int = attachments.length;
				var iDocument:IDocument = radiate.selectedDocument;
				
				if (event.successful) {
					
				}
				else {
					Radiate.warn("Upload not successful for some reason");
				}
				
				// cause the list to update and rerender the rows
				libraryCollection.refresh();
			}
			
			protected function uploadAttachmentFault(event:RadiateEvent):void {
				//var message:String = event.message;
				Radiate.error("Upload error: "+event.toString());
			}
			
			protected function refreshIcon_clickHandler(event:MouseEvent):void {
				//var projectOrDocumentID:String = radiate.selectedProject ? radiate.selectedProject.id : null;
				var projectOrDocumentID:String = radiate.selectedDocument ? radiate.selectedDocument.id : null;
				var searchID:int;
				
				// if not project id then get document id
				if (projectOrDocumentID==null) {
					projectOrDocumentID = radiate.selectedProject ? radiate.selectedProject.id : null;
				}
				
				if (projectOrDocumentID==null) {
					searchID = 0;
				}
				else {
					searchID = int(projectOrDocumentID);
				}
				
				radiate.getAttachments(searchID);
			}
			
			protected function assetAdded(event:Event):void {
				updateAssets();
			}
			
			protected function assetRemoved(event:Event):void {
				updateAssets();
			}
			
			private function updateAssets():void {
				// to do filter assets by document
				
				// not sure why it's not deleting the last item
				if (radiate.assets.length==0) {
					// RangeError: Index '-1' specified is out of bounds. 
					// - when setting source to empty array before hand 
					// - assets.source may be bound so when that array item is 
					// - removed and then remove all is called it may not be found
					// - calling refresh before calling remove all
					libraryCollection.refresh();
					libraryCollection.removeAll();
				}
				else {
					if (libraryCollection.source != radiate.assets.source) {
						libraryCollection.source = radiate.assets.source;
						libraryCollection.refresh();
					}
					else {
						libraryCollection.refresh();
					}
				}
			}
			
			protected function library_dragEnterHandler(event:DragEvent):void {
				var dragSource:DragSource = event.dragSource;
				
				if (Radiate.isAcceptableDragAndDropFormat(dragSource, true)) {
					DragManager.acceptDragDrop(IUIComponent(event.currentTarget));
				}
			}
			
			protected function library_dragOverHandler(event:DragEvent):void {
				
				if (Radiate.isAcceptableDragAndDropFormat(event.dragSource, true)) {
					DragManager.acceptDragDrop(IUIComponent(event.currentTarget));
				}
			}
			
			public function library_dragDropHandler(event:DragEvent):void {
				var initiator:UIComponent;
				var snapshotTest:Boolean;
				var snapshot:Object;
				var imageData:ImageData;
				var format:String;
				var dragSource:DragSource;
				var hasURLFormat:Boolean;
				var hasFileListFormat:Boolean;
				var hasFilePromiseListFormat:Boolean;
				var isSelf:Boolean;
				
				if (event.dragInitiator) {
					isSelf = verticalContainer.contains(event.dragInitiator as DisplayObject);
					if (isSelf) {
						return;
					}
				}
				
				dragSource = event.dragSource;
				
				if (dragSource.hasFormat("UIComponent")) {
					imageData = new ImageData();
					initiator = event.dragInitiator as UIComponent;
					
					// initiator will always be UIComponent with current drag manager
					if (initiator) {
						
						if (snapshotTest) {
							snapshot = DisplayObjectUtils.getSnapshotWithQuality(initiator);
						}
						else {
							//if (initiator is IUIComponent) {
								snapshot = DisplayObjectUtils.getUIComponentBitmapData(initiator);
							//}
							//else if (initiator is GraphicElement) {
							//	snapshot = DisplayObjectUtils.getGraphicElementBitmapData(initiator as IGraphicElement);
							//}
						}
						
						if (snapshot is BitmapData) {
							imageData.bitmapData = snapshot as BitmapData;
							imageData.byteArray = DisplayObjectUtils.getBitmapByteArray(snapshot as BitmapData);
							imageData.name = ClassUtils.getIdentifierNameOrClass(initiator) + ".png";
							imageData.contentType = DisplayObjectUtils.PNG_MIME_TYPE;
							imageData.file = null;
							
							radiate.addAssetToDocument(imageData, radiate.selectedDocument);
							
							list.selectedItem = imageData;
						}
						else {
							Radiate.error("Could not create a snapshot of the selected item. " + snapshot); 
						}
					}
				}
			}
			
			public function list_keyUpHandler(event:KeyboardEvent):void {
				if (event.keyCode==Keyboard.SPACE) {
					// show full view of image
					//trace("space button pressed");
					var imageData:ImageData = list.selectedItem as ImageData;
					
					if (imageData) {
						if (imageData.bitmapData) {
							openPopUp.data = imageData.bitmapData;
						}
						else if (imageData.url) {
							openPopUp.data = imageData.url;
						}
					}
					
					if (!openPopUp.isOpen) {
						openPopUp.play();
					}
					else {
						openPopUp.close();
					}
					
				}
				if (event.keyCode==Keyboard.ESCAPE) {
					openPopUp.close();
				}
			}
			
			protected function openInBrowserButton_clickHandler(event:MouseEvent):void
			{
				var imageData:ImageData = list.selectedItem as ImageData;
				
				if (imageData && imageData.url) {
					Radiate.openInBrowser(imageData);
				}
				else {
					Radiate.warn("You must upload the file before being able to view it online.");
				}
			}
			
			/**
			 * DRAG TO DESKTOP CODE
			 * http://help.adobe.com/en_US/flex/using/WS2db454920e96a9e51e63e3d11c0bf64595-7fee.html
			 
			private function onMouseDown(event:MouseEvent):void { 
				var fileURL:String = "app:/image.jpg"; 
				var display:Bitmap ; 
				var bitmapFile:File = new File(fileURL); 
				var transferObject:Clipboard = createClipboard(display, bitmapFile); 
				NativeDragManager.doDrag(this,transferObject, display.bitmapData, new Point(-mouseX,-mouseY)); 
			} 
			
			public function createClipboard(image:Bitmap, sourceFile:File):Clipboard { 
				var transfer:Clipboard = new Clipboard(); 
				transfer.setData("bitmap",  
					image,  
					true);  
				// ActionScript 3 Bitmap object by value and by reference 
				transfer.setData(ClipboardFormats.BITMAP_FORMAT,  
					image.bitmapData,  
					false);  
				// Standard BitmapData format 
				transfer.setData(ClipboardFormats.FILE_LIST_FORMAT,  
					new Array(sourceFile),  
					false);  
				// Standard file list format 
				return transfer;  
			} 
			*/
			
			public function libraryFilterFunction(item:Object):Boolean {
				var imageData:ImageData = item as ImageData;
				var imageName:String = imageData ? imageData.name : "";
				var imageParentId:int = imageData ? int(imageData.parentId) : -1;
				var searchText:String;
				
				if (imageParentId!=documentID && imageParentId!=projectID) {
					return false;
				}
				
				searchText = searchCollection.text.toLowerCase();
				
				if (imageName.indexOf(searchText)==-1) {
					return false;
				}
				
				return true;
			}
			
			protected function documentChanged(event:RadiateEvent):void {
				var documentData:DocumentData = event.selectedItem as DocumentData;
				
				documentID = documentData && documentData.id!=null ? int(documentData.id) : 0;
				projectID = documentData && documentData.parentId!=null ? int(documentData.parentId) : 0;
				
			}
			
			private var documentID:int;
			private var projectID:int;
			
			protected function openPopUp_closeHandler(event:Event):void
			{
				if (openPopUp.popUp) {
					var imageView:ImageView = openPopUp.popUp as ImageView;
					if (imageView) {
						imageView.data = null;
					}
					openPopUp.popUp = null;
				}
					
			}
			
			protected function handleDragDropIncomplete(event:DragDropEvent):void
			{
				 //trace("Drag drop incomplete");
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:ArrayCollection id="libraryCollection"/>
		
		<!-- SORT BY NAME -->
		<handlers:EventHandler eventName="creationComplete" >
			<collections:SortCollection target="{libraryCollection}" fields="{['name']}" />
		</handlers:EventHandler>

		
		<!-- PREVIEW -->
		<fc:OpenPopUp id="openPopUp" 
						 modalDuration="250"
						 showDropShadow="true"
						 popUpType="{ImageView}"
						 autoCenter="true"
						 fitMaxSizeToApplication="true"
						 percentWidth="80"
						 percentHeight="80"
						 closePreviousInstanceIfOpen="true"
						 closeOnMouseDownOutside="true"
						 closeOnMouseDownInside="true"
						 close="openPopUp_closeHandler(event)"/>
		
		<!-- COPY TO THE CLIPBOARD -->
		<handlers:EventHandler eventName="click" target="{copyIcon}" setTriggerEvent="true">
			
			<clipboard:CopyToClipboard id="copyToClipboard" data="{imageURLInput.text}" targetAncestor="{this}" allowNullData="true">
				<clipboard:successEffect>
					<status:ShowStatusMessage message="URL copied to the clipboard"/>
				</clipboard:successEffect>
				<clipboard:noDataEffect>
					<status:ShowStatusMessage message="Nothing to copy to the clipboard"/>
				</clipboard:noDataEffect>
				<clipboard:errorEffect>
					<status:ShowStatusMessage message="An error occurred while attempting to copy to the clipboard"/>
				</clipboard:errorEffect>
			</clipboard:CopyToClipboard>
			
		</handlers:EventHandler>
		
		<!-- BROWSE FOR FILE --> 
		<handlers:EventHandler eventName="click" targets="{[browseForFileIcon,addImageLabel]}">
			<file:BrowseForFile id="browseForFile"
								fileTypes="png,jpg,jpeg,gif"
								targetAncestor="{this}"
								allowMultipleSelection="false">
				<file:selectionEffect>
					<file:LoadFile id="loadFile" 
								   loadIntoLoader="true"
								   fileReference="{browseForFile.fileReference}"
								   loaderComplete="loadFile_completeHandler(event)"
								   >
						
					</file:LoadFile>
				</file:selectionEffect>
			</file:BrowseForFile>
		</handlers:EventHandler>
	  
		<!-- SHOW DELETE RESOURCE POP UP -->
		<handlers:EventHandler targets="{deleteAttachmentIcon}" eventName="click" 
							   enabled="{list.selectedItem!=null}">
			<fc:OpenPopUp id="openDeleteDocumentPopUp" 
						  popUpType="{DeleteDocumentWindow}" 
						  modalDuration="100" 
						  percentWidth="75"
						  percentHeight="90"
						  width="600"
						  parent="{parentApplication}"
						  popUpOptions="{{currentState:DeleteDocumentWindow.RESOURCE, documentData:list.selectedItem}}"
						  />
		</handlers:EventHandler>
	</fx:Declarations>
	
	
	<mx:VDividedBox id="verticalContainer" 
					width="100%" height="100%"
					top="0"
					left="8" 
					right="8" 
					bottom="4"
					>
		
		<inspectors:SearchCollection id="searchCollection" 
									 minHeight="24"
									 maxHeight="60"
									 dataProvider="{libraryCollection}" 
									 filterFunction="libraryFilterFunction"
									 >
		</inspectors:SearchCollection>
		
		
		
		<s:List id="list" 
				width="100%" 
				height="100%"
				labelField="name" 
				dragEnabled="true"
				borderVisible="false"
				interactionMode="mouse"
				requireSelection="true"
				dataProvider="{libraryCollection}"
				change="list_changeHandler(event)"
				itemRenderer="com.flexcapacitor.views.renderers.LibraryInspectorRenderer"
				keyUp="list_keyUpHandler(event)"
				>
		</s:List>
	
		
	</mx:VDividedBox>
	
	<s:BorderContainer width="100%" borderWeight="0" borderVisible="false">
		<s:layout>
			<s:VerticalLayout />
		</s:layout>
		
		<s:Label text="There are no items in the library"
				 color="#888888"
				 horizontalCenter="0" verticalCenter="0"
				 visible="{libraryCollection.length==0}" 
				 includeInLayout="{libraryCollection.length==0}"
				 width="100%"
				 textAlign="center"
				 />
		
		<c:LabelButton id="addImageLabel" 
					   text="Click here to add an image to the library or drag and drop images onto the app"
					   color="#888888"
					   paddingTop="50"
					   paddingBottom="20"
					   paddingLeft="10"
					   paddingRight="10"
					   horizontalCenter="0" verticalCenter="0"
					   visible="{libraryCollection.length==0}" 
					   includeInLayout="{libraryCollection.length==0}"
					   width="100%"
					   textAlign="center"
					   />
		
		<s:Spacer height="100%"/>
		
		<s:HGroup width="100%"
				 height="24"
				 minHeight="24"
				 verticalAlign="middle"
				 >
			
			<c:BeveledImageButton id="browseForFileIcon" 
					 source="{Radii8LibraryAssets.newFile}" 
					 toolTip="Add an image to the document library"
					 />
			
			<c:BeveledImageButton id="openInBrowserButton" 
								  source="{Radii8LibraryEffectsAssets.play}" 
								  toolTip="View in the browser"
								  click="openInBrowserButton_clickHandler(event)"
								  />
			
			
			<s:TextInput id="imageURLInput" 
						   left="0" right="0" top="0" 
						   width="100%"
						   minWidth="60" 
						   prompt=""
						   styleName="inputStyles"
						   includeInLayout="false"
						   visible="false"
						   />
			
			<s:HGroup verticalAlign="bottom"
					  includeInLayout="true"
					  visible="false"
					  width="100%">
				<s:Spacer width="4"/>
				<s:BorderContainer id="thumnailImage" 
								   width="12" height="12"
								   backgroundColor="#cccccc"
								   borderVisible="false"
								   useHandCursor="true"
								   buttonMode="true"
								   toolTip="Thumbnail URL"
								   click="thumbnailImage_clickHandler(event)"
								   />
				
				<!--<s:BorderContainer id="smallImage" 
								   width="12" height="12"
								   backgroundColor="gray"
								   borderVisible="false"
								   useHandCursor="true"
								   buttonMode="true"
								   toolTip="Small Image URL"
								   click="smallImage_clickHandler(event)"
								   />-->
				<s:BorderContainer id="mediumImage" 
								   width="14" height="14"
								   backgroundColor="#cccccc"
								   borderVisible="false"
								   useHandCursor="true"
								   buttonMode="true"
								   toolTip="Medium Image URL"
								   click="mediumImage_clickHandler(event)"
								   />
				
				<s:BorderContainer id="fullImage" 
								   width="14" height="16"
								   backgroundColor="#cccccc"
								   borderVisible="false"
								   useHandCursor="true"
								   buttonMode="true"
								   toolTip="Original Image URL"
								   click="fullImage_clickHandler(event)"
								   />
				
				
			</s:HGroup>
			<c:BeveledImageButton id="copyIcon" 
					 source="{Radii8LibraryAssets.copy}" 
					 toolTip="Copy the image URL to the Clipboard"/>
			
			<c:BeveledImageButton id="refreshIcon" 
					 source="{Radii8LibraryAssets.refresh}" 
					 toolTip="Refresh"
					 click="refreshIcon_clickHandler(event)"/>
			
			<c:BeveledImageButton id="deleteAttachmentIcon" 
						   source="{Radii8LibraryAssets.trashCan}" 
						   toolTip="Remove Attachment"
						   />
		</s:HGroup>
	</s:BorderContainer>
	
</s:VGroup>
