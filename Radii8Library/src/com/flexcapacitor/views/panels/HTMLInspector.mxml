<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 
		 paddingTop="8"
		 paddingBottom="8"
		 paddingLeft="8"
		 paddingRight="8"
		 width="100%" 
		 height="100%"
		 styleName="inspectablePanel"
		 implements="com.flexcapacitor.views.IInspector" 
		 >
	
	
	<fx:Script>
		<![CDATA[
			import com.flexcapacitor.controller.Radiate;
			import com.flexcapacitor.events.RadiateEvent;
			import com.flexcapacitor.model.IDocument;
			import com.flexcapacitor.utils.MXMLDocumentConstants;
			import com.flexcapacitor.utils.StringUtils;
			import com.flexcapacitor.utils.supportClasses.ComponentDescription;
			
			import mx.collections.ArrayCollection;
			
			import flashx.textLayout.conversion.ITextImporter;
			import flashx.textLayout.conversion.TextConverter;
			import flashx.textLayout.elements.Configuration;
			import flashx.textLayout.elements.TextFlow;
			
			
			private var radiate:Radiate = Radiate.instance;
			private var settingTarget:Boolean;
			
			[Bindable]
			public var target:Object;
			public var componentDescription:ComponentDescription;
			
			public var anchorTargetsCollection:ArrayCollection;
			public var anchorTargets:Array = ["_blank", "_parent", "_self", "_top"];
			
			public function activate(selectedTarget:Object = null):void {
				
				if (!anchorTargetsCollection) {
					anchorTargetsCollection = new ArrayCollection(anchorTargets);
					targetsComboBox.dataProvider = anchorTargetsCollection;
				}
				
				if (radiate.target) {
					targetChangedHandler();
				}
				
				radiate.addEventListener(RadiateEvent.TARGET_CHANGE, targetChangedHandler, false, 0, true);
				radiate.addEventListener(RadiateEvent.HISTORY_CHANGE, targetChangedHandler, false, 0, true);
				
				
			}
			
			public function deactivate():void {
				if (radiate) {
					radiate.removeEventListener(RadiateEvent.TARGET_CHANGE, targetChangedHandler);
					radiate.removeEventListener(RadiateEvent.HISTORY_CHANGE, targetChangedHandler);
				}
				
				target = null;
				componentDescription = null;
			}
			
			
			protected function targetChangedHandler(event:RadiateEvent = null):void {
				if (settingTarget) return;
				var iDocument:IDocument = radiate.selectedDocument;
				
				if (iDocument==null) {
					enabled = false;
					return;
				}
				else {
					enabled = true;
				}
				
				if (radiate.target!=target) {
					target = radiate.target;
					componentDescription = iDocument.getItemDescription(target);
				}
				
				if (componentDescription) {
					
					embedSnapshopIntoBackground.selected = componentDescription.createBackgroundSnapshot;
					convertElementToImage.selected = componentDescription.convertElementToImage;
					
					wrapWithAnchor.selected = componentDescription.wrapWithAnchor;
					targetsComboBox.selectedItem = componentDescription.anchorTarget;
					urlTextInput.text = componentDescription.anchorURL;
					
					tagNameTextInput.text = componentDescription.htmlTagName;
					
					
					var config:Configuration = new Configuration();
					var parser:ITextImporter;
					
					config.manageTabKey = true;
					parser = TextConverter.getImporter(TextConverter.PLAIN_TEXT_FORMAT, config);
					
					htmlBefore.textFlow = parser.importToFlow(componentDescription.htmlBefore);
					htmlAfter.textFlow = parser.importToFlow(componentDescription.htmlAfter);
					htmlOverride.textFlow = parser.importToFlow(componentDescription.htmlOverride);
					
					(htmlBefore.textFlow.configuration as Configuration).manageTabKey = true;
					(htmlAfter.textFlow.configuration as Configuration).manageTabKey = true;
					(htmlOverride.textFlow.configuration as Configuration).manageTabKey = true;
					
					htmlAttributes.text = componentDescription.htmlAttributes;
					htmlStyles.text = componentDescription.userStyles;
					
					embedSnapshopIntoBackground.enabled = componentDescription.isVisualElementContainer;
				}
				
			}
			
			public var componentDescriptor:ComponentDescription;
			
			protected function componentCSS_changeHandler(event:Event):void {
				var iDocument:IDocument = radiate.selectedDocument;
				var prevTarget:Object = radiate.target;
				
				if (componentDescription==null && radiate.target) {
					componentDescription = iDocument.getItemDescription(target);
				}
				
				settingTarget = true;
				if (componentDescription) {
					Radiate.setProperty(componentDescription, "userStyles", htmlStyles.text);
					Radiate.setTarget(prevTarget, true, "", true);
				}
				settingTarget = false;
			}
			
			protected function wrapWithAnchor_changeHandler(event:Event):void {
				var iDocument:IDocument = radiate.selectedDocument;
				var prevTarget:Object = radiate.target;
				
				if (componentDescription==null && radiate.target) { 
					componentDescription = iDocument.getItemDescription(target);
				}
				
				settingTarget = true;
				
				if (componentDescription) {
					Radiate.setProperty(componentDescription, "wrapWithAnchor", wrapWithAnchor.selected);
				
					if (wrapWithAnchor.selected) {
						Radiate.setProperty(componentDescription, "anchorURL", urlTextInput.text);
						if (event.type=="textInput") {
							Radiate.setProperty(componentDescription, "anchorTarget", targetsComboBox.textInput.text);
						}
						else {
							Radiate.setProperty(componentDescription, "anchorTarget", targetsComboBox.selectedItem);
						}
					}
					else {
						Radiate.clearProperty(componentDescription, "anchorURL");
						Radiate.clearProperty(componentDescription, "anchorTarget");
					}
					
					Radiate.setTarget(prevTarget, true, "", true);
				}
				
				settingTarget = false;
			}
			
			protected function embedSnapshopIntoBackground_changeHandler(event:Event):void {
				var iDocument:IDocument;
				var prevTarget:Object = radiate.target;
				
				if (prevTarget) {
					iDocument = radiate.selectedDocument;
					componentDescription = iDocument.getItemDescription(target);
				}
				
				if (componentDescription) {
					settingTarget = true;
					Radiate.setProperty(componentDescription, "createBackgroundSnapshot", embedSnapshopIntoBackground.selected);
					Radiate.setTarget(prevTarget, true, "", true);
					settingTarget = false;
				}
			}
			
			protected function convertElementToImage_changeHandler(event:Event):void {
				var iDocument:IDocument;
				var prevTarget:Object = radiate.target;
				
				if (radiate.target) {
					iDocument = radiate.selectedDocument;
					componentDescription = iDocument.getItemDescription(target);
				}
				
				if (componentDescription) {
					settingTarget = true;
					Radiate.setProperty(componentDescription, "convertElementToImage", convertElementToImage.selected);
					Radiate.setTarget(prevTarget, true, "", true);
					settingTarget = false;
				}
			}
			
			protected function customTagName_changeHandler(event:Event):void {
				var iDocument:IDocument = radiate.selectedDocument;
				var prevTarget:Object = radiate.target;
				var value:String;
				
				if (componentDescription==null && radiate.target) { 
					componentDescription = iDocument.getItemDescription(target);
				}
				
				settingTarget = true;
				
				if (componentDescription) {
					//Radiate.setProperty(componentDescription, "htmlTagName", wrapWithAnchor.selected);
					value = StringUtils.trim(tagNameTextInput.text);
					
					if (value!="") {
						Radiate.setProperty(componentDescription, "htmlTagName", value);
					}
					else {
						Radiate.clearProperty(componentDescription, "htmlTagName");
					}
					
					Radiate.setTarget(prevTarget, true, "", true);
				}
				
				settingTarget = false;
			}
			
			protected function htmlOverride_changeHandler(event:Event):void {
				var iDocument:IDocument = radiate.selectedDocument;
				var prevTarget:Object = radiate.target;
				var value:String;
				
				if (componentDescription==null && radiate.target) { 
					componentDescription = iDocument.getItemDescription(target);
				}
				
				settingTarget = true;
				
				if (componentDescription) {
					//Radiate.setProperty(componentDescription, "htmlTagName", wrapWithAnchor.selected);
					value = StringUtils.trim(htmlOverride.text);
					
					if (value!="") {
						Radiate.setProperty(componentDescription, MXMLDocumentConstants.HTML_OVERRIDE, value);
					}
					else {
						Radiate.clearProperty(componentDescription, MXMLDocumentConstants.HTML_OVERRIDE);
					}
					
					Radiate.setTarget(prevTarget, true, "", true);
				}
				
				settingTarget = false;
			}
			
			protected function htmlBefore_changeHandler(event:Event):void {
				var iDocument:IDocument = radiate.selectedDocument;
				var prevTarget:Object = radiate.target;
				var value:String;
				
				if (componentDescription==null && radiate.target) { 
					componentDescription = iDocument.getItemDescription(target);
				}
				
				settingTarget = true;
				
				if (componentDescription) {
					//Radiate.setProperty(componentDescription, "htmlTagName", wrapWithAnchor.selected);
					value = StringUtils.trim(htmlBefore.text);
					
					if (value!="") {
						Radiate.setProperty(componentDescription, MXMLDocumentConstants.HTML_BEFORE, value);
					}
					else {
						Radiate.clearProperty(componentDescription, MXMLDocumentConstants.HTML_BEFORE);
					}
					
					Radiate.setTarget(prevTarget, true, "", true);
				}
				
				settingTarget = false;
			}
			
			protected function htmlAfter_changeHandler(event:Event):void {
				var iDocument:IDocument = radiate.selectedDocument;
				var prevTarget:Object = radiate.target;
				var value:String;
				
				if (componentDescription==null && radiate.target) { 
					componentDescription = iDocument.getItemDescription(target);
				}
				
				settingTarget = true;
				
				if (componentDescription) {
					//Radiate.setProperty(componentDescription, "htmlTagName", wrapWithAnchor.selected);
					value = StringUtils.trim(htmlAfter.text);
					
					if (value!="") {
						Radiate.setProperty(componentDescription, MXMLDocumentConstants.HTML_AFTER, value);
					}
					else {
						Radiate.clearProperty(componentDescription, MXMLDocumentConstants.HTML_AFTER);
					}
					
					Radiate.setTarget(prevTarget, true, "", true);
				}
				
				settingTarget = false;
			}
			
			protected function attributes_changeHandler(event:Event):void {
				var iDocument:IDocument = radiate.selectedDocument;
				var prevTarget:Object = radiate.target;
				
				if (componentDescription==null && radiate.target) {
					componentDescription = iDocument.getItemDescription(target);
				}
				
				settingTarget = true;
				if (componentDescription) {
					Radiate.setProperty(componentDescription, MXMLDocumentConstants.HTML_ATTRIBUTES, htmlAttributes.text);
					Radiate.setTarget(prevTarget, true, "", true);
				}
				settingTarget = false;
			}
			
			
		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="HTML"/>
		<s:State name="MXML"/>
		<s:State name="ANDROID"/>
	</s:states>
	
	<s:CheckBox id="embedSnapshopIntoBackground" 
				label="Embed snapshot into element background" 
				change="embedSnapshopIntoBackground_changeHandler(event)"/>
	<s:CheckBox id="convertElementToImage" 
				label="Convert element into image" 
				change="convertElementToImage_changeHandler(event)"/>
	<s:Spacer />
	<s:HGroup width="100%" verticalAlign="baseline" gap="5">
		<s:Label id="customTagName" 
					text="Tag Name" />
		<s:TextInput id="tagNameTextInput" 
					 height="22" 
					 width="100%" 
					 prompt="default"
					 change="customTagName_changeHandler(event)"
					 />
	</s:HGroup>
	
	<mx:TabNavigator id="firstViewStack" 
					 borderStyle="solid"
					 width="100%"
					 height="100%"
					 selectedIndex="0"
					 creationPolicy="all">
		
		<!-- CSS -->
		<s:NavigatorContent width="100%" height="100%" label="CSS">
			<s:layout>
				<s:VerticalLayout padding="4"/>
			</s:layout>
			
			<s:TextArea id="htmlStyles" 
						width="100%" height="100%"
						prompt="Enter your own CSS here" 
						toolTip="(Separate each by a semicolon and new line)"
						focusOut="componentCSS_changeHandler(event)"
						change="componentCSS_changeHandler(event)"
						borderVisible="false">
				
			</s:TextArea>
		</s:NavigatorContent>
		
		<!-- ATTRIBUTES -->
		<s:NavigatorContent width="100%" height="100%" label="Attributes">
			<s:layout>
				<s:VerticalLayout padding="4"/>
			</s:layout>
			<s:TextArea id="htmlAttributes" 
						width="100%" height="100%"
						prompt="Enter your own attributes here" 
						toolTip="(Separate each by a new line)"
						focusOut="attributes_changeHandler(event)"
						change="attributes_changeHandler(event)"
						borderVisible="false">
				
			</s:TextArea>
		</s:NavigatorContent>
		
		<!-- HTML BEFORE -->
		<s:NavigatorContent width="100%" height="100%" label="Before">
			<s:layout>
				<s:VerticalLayout padding="4"/>
			</s:layout>
			<s:TextArea id="htmlBefore" 
						width="100%" height="100%"
						tabStops="10"
						prompt="Enter HTML before content here" 
						toolTip="Adds HTML before generated output"
						focusOut="htmlBefore_changeHandler(event)"
						change="htmlBefore_changeHandler(event)"
						borderVisible="false">
				
			</s:TextArea>
		</s:NavigatorContent>
		
		<!-- HTML AFTER -->
		<s:NavigatorContent width="100%" height="100%" label="After">
			<s:layout>
				<s:VerticalLayout padding="4"/>
			</s:layout>
			<s:TextArea id="htmlAfter" 
						width="100%" height="100%"
						tabStops="10"
						prompt="Enter HTML after content here" 
						toolTip="Adds HTML after generated output"
						focusOut="htmlAfter_changeHandler(event)"
						change="htmlAfter_changeHandler(event)"
						borderVisible="false">
				
			</s:TextArea>
		</s:NavigatorContent>
		
		<!-- HTML Override -->
		<s:NavigatorContent width="100%" height="100%" label="Override">
			<s:layout>
				<s:VerticalLayout padding="4"/>
			</s:layout>
			<s:TextArea id="htmlOverride" 
						width="100%" height="100%"
						tabStops="10"
						prompt="Enter your own HTML here" 
						toolTip="Overrides all generated HTML output"
						focusOut="htmlOverride_changeHandler(event)"
						change="htmlOverride_changeHandler(event)"
						borderVisible="false">
				
			</s:TextArea>
		</s:NavigatorContent>
	</mx:TabNavigator>
	
	<s:HGroup width="100%" verticalAlign="baseline" gap="5">
		<s:CheckBox id="wrapWithAnchor" 
					label="Wrap in hyperlink" 
					change="wrapWithAnchor_changeHandler(event)"/>
		<s:ComboBox id="targetsComboBox" 
					height="22" 
					width="80" 
					enabled="{wrapWithAnchor.selected}"
					textInput="wrapWithAnchor_changeHandler(event)"
					change="wrapWithAnchor_changeHandler(event)"/>
		<s:TextInput id="urlTextInput" 
					 height="22" 
					 width="100%" 
					 prompt="URL"
					 enabled="{wrapWithAnchor.selected}"
					 change="wrapWithAnchor_changeHandler(event)"
					/>
	</s:HGroup>

</s:VGroup>
