<?xml version="1.0" encoding="utf-8"?>
<s:HGroup xmlns:graphics="be.nascom.flash.graphics.*"
		 xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:flexcapacitor="flexcapacitor.*"
		 xmlns:c="com.flexcapacitor.controls.*"
		 
		 paddingLeft="4"
		 creationComplete="group1_creationCompleteHandler(event)"
		 fontSize="9"
		 >
	
	<fx:Script>
		<![CDATA[
			import com.flexcapacitor.controller.Radiate;
			import com.flexcapacitor.events.RadiateEvent;
			import com.flexcapacitor.managers.ComponentManager;
			import com.flexcapacitor.managers.DocumentManager;
			import com.flexcapacitor.managers.KeyboardManager;
			import com.flexcapacitor.managers.ScaleManager;
			import com.flexcapacitor.managers.ToolManager;
			import com.flexcapacitor.model.IDocument;
			import com.flexcapacitor.tools.ITool;
			import com.flexcapacitor.tools.Selection;
			import com.flexcapacitor.tools.Zoom;
			import com.flexcapacitor.utils.supportClasses.ComponentDescription;
			
			import mx.events.FlexEvent;
			
			import spark.components.Application;
			import spark.components.Scroller;
			import spark.components.supportClasses.GroupBase;
			import spark.effects.Animate;
			import spark.effects.animation.MotionPath;
			import spark.effects.animation.SimpleMotionPath;
			import spark.events.IndexChangeEvent;
			
			public var radiate:Radiate;
			
			public var zoomTool:Zoom;
			
			public var toolDescription:ComponentDescription;
			
			/**
			 * Percent to scale. Default is 10 for 10%. 
			 **/
			public var scaleDeltaPercent:int = 10;
			
			/**
			 * Shows the show actual size button
			 * */
			[Bindable]
			public var showActualSize:Boolean = true;
			
			protected function group1_creationCompleteHandler(event:FlexEvent):void {
				radiate = Radiate.instance;
				
				radiate.addEventListener(RadiateEvent.SCALE_CHANGE, scaleChangedHandler, false, 0, true);
				radiate.addEventListener(RadiateEvent.DOCUMENT_CHANGE, documentChangeHandler, false, 0, true);
				
				toolDescription = ToolManager.getToolByType(Zoom);
				
				zoomTool = toolDescription.instance as Zoom;
				
				
				// temp to get reference
				//if (!tool) tool = new Zoom();
				
				updateTool(zoomTool);
			}
			
			/**
			 * Update UI components to tool value
			 * */
			public function updateTool(tool:ITool):void {
				var zoomValue:Number;
				zoomValue = zoomTool.getScale();
				
				updateZoomInComboBox(zoomValue);
			}
			
			/**
			 * Zoom combo box change handler. Update zoom level. 
			 * */
			protected function zoomComboBox_changeHandler(event:IndexChangeEvent):void {
				
				zoomTool.setScale(parseInt(zoomComboBox.selectedItem)/100);
			}
			
			/**
			 * 
			 * */
			protected function actualSize_clickHandler(event:MouseEvent):void {
				var currentScale:Number = ScaleManager.getScale();
				
				if (zoomTool) {
					zoomTool.restoreDefaultScale();
					zoomComboBox.selectedItem = "100%";
				}
				else {
					ScaleManager.restoreDefaultScale();
				}
				
				if (animate) {
					animateIntoView(1, currentScale);
				}
			}
			
			public var animate:Boolean;
			public var scaleAnimation:Animate;
			public var scaleAnimationDuration:int = 250;
			
			public function animateIntoView(newScale:Number, oldScale:Number = NaN):void {
				var newPoint:Point;
				var scrollMotionPaths:Vector.<MotionPath>;
				var horizontalScale:SimpleMotionPath;
				var verticalScale:SimpleMotionPath;
				var selectedDocument:IDocument = Radiate.selectedDocument;
				
				if (!selectedDocument) {
					return;
				}
				
				scaleAnimation = new Animate();
				//scrollBarAnimation.addEventListener(EffectEvent.EFFECT_END, hideScrollBarAnimation_effectEndHandler);
				scaleAnimation.duration = scaleAnimationDuration;
				horizontalScale = new SimpleMotionPath("scaleX", oldScale, newScale);
				verticalScale = new SimpleMotionPath("scaleY", oldScale, newScale);
				scrollMotionPaths = Vector.<MotionPath>([horizontalScale, verticalScale]);
				scaleAnimation.motionPaths = scrollMotionPaths;
				scaleAnimation.play([selectedDocument.instance]);
			}
			
			/**
			 * Scale changed
			 * */
			protected function scaleChangedHandler(event:RadiateEvent):void {
				var zoomValue:Number;
				var zoomString:String;
				
				zoomValue = zoomTool.getScale();
				
				updateZoomInComboBox(zoomValue);
			}
			
			/**
			 * Format zoom value nicely.
			 * */
			public function updateZoomInComboBox(zoomValue:Number):void {
				var zoomString:String;
				
				if (!isNaN(zoomValue)) {
					
					if (String(zoomValue*100).indexOf(".")!=-1) {
						zoomString = Number(zoomValue*100).toFixed(1) + "%";
					}
					else {
						zoomString = Number(zoomValue*100) + "%";
					}
					
					if (zoomValue==1) {
						zoomComboBox.selectedItem = "100%";
					}
					else {
						zoomComboBox.selectedItem = zoomString;
					}
				}
			}
			
			/**
			 * Document changed
			 * */
			protected function documentChangeHandler(event:RadiateEvent):void {
				// since we use this independently of the tool being selected we need to make sure
				// the target application is set
				// we'll move this later
				var zoomValue:Number = zoomTool.getScale();
				
				updateZoomInComboBox(zoomValue);
			}
			
			/**
			 * Restore to 100%
			 * */
			protected function zoomComboBox_doubleClickHandler(event:MouseEvent):void {
				
				if (zoomTool) {
					zoomTool.restoreDefaultScale();
					zoomComboBox.selectedItem = "100%";
				}
			}
			
			protected function fitInButton_clickHandler(event:MouseEvent):void {
				var scroller:Scroller;
				var target:Application;
				var ws:Number;
				var hs:Number;
				var selectedDocument:IDocument = Radiate.selectedDocument;
				
				// disabled animation until bugs can be worked out
				if (animate && selectedDocument) {
					scroller = DocumentManager.canvasScroller;
					target = selectedDocument.instance as Application;
					
					if (target && selectedDocument) {
						//ws = scroller.width/scroller.viewport.contentWidth;
						//hs = scroller.height/scroller.viewport.contentHeight;
						ws = target.width/target.contentGroup.contentWidth;
						hs = target.height/target.contentGroup.contentHeight;
						
						fitToScale.scaleXTo = ws;
						fitToScale.scaleYTo = hs;
						fitToScale.duration = 250;
						fitToScale.play([selectedDocument.instance]);
					}
				}
				else {
					ScaleManager.scaleToFit(true);
					DocumentManager.centerDocumentInViewport(true, true);
				}
			}
			
			protected function scrollToTopButton_clickHandler(event:MouseEvent):void {
				DocumentManager.centerDocumentInViewport(true, true, true);
			}
			
			protected function button_mouseUpHandler(event:MouseEvent):void {
				if (Radiate.target) {
					Selection.instance.updateSelection(Radiate.target);
				}

				currentScrollPoint = null;
			}
			
			protected function zoomOutButton_buttonDownHandler(event:FlexEvent):void {
				var point:Point;
				
				if (Radiate.selectedDocument==null) return;
				
				if (KeyboardManager.shiftKeyDown && Radiate.target) {
					point = ComponentManager.getPositionPoint(Radiate.target, true, true);
					ScaleManager.decreaseScaleBy(scaleDeltaPercent, point);
				}
				else if (centerOnCurrentPoint && currentScrollPoint) {
					ScaleManager.decreaseScaleBy(scaleDeltaPercent, currentScrollPoint);
				}
				else if (offsetOnZoomIn) {
					ScaleManager.decreaseScaleBy(scaleDeltaPercent);
				}
				else {
					ScaleManager.decreaseScaleBy(scaleDeltaPercent);
				}
			}
			
			protected function zoomInButton_buttonDownHandler(event:FlexEvent):void {
				var point:Point;
				var scrollPoint:Point;
				var newScrollPoint:Point;
				var horizontalOffset:Point;
				var iDocument:IDocument = Radiate.selectedDocument;
				var currentScrollRectangle:Rectangle;
				
				if (iDocument==null) return;
				
				if (KeyboardManager.shiftKeyDown && Radiate.target) {
					point = ComponentManager.getPositionPoint(Radiate.target, true, true);
					ScaleManager.increaseScaleBy(scaleDeltaPercent, point);
				}
				else if (centerOnCurrentPoint && currentScrollPoint) {
					ScaleManager.increaseScaleBy(scaleDeltaPercent, currentScrollPoint);
				}
				else if (offsetOnZoomIn) {
					scrollPoint = DocumentManager.getMaxScrollPoint();
					ScaleManager.increaseScaleBy(scaleDeltaPercent);
					
					newScrollPoint = DocumentManager.getMaxScrollPoint();
					horizontalOffset = newScrollPoint.subtract(scrollPoint);
					currentScrollRectangle = DocumentManager.getScrollRectangle(DocumentManager.getDocumentScroller(iDocument).viewport as GroupBase);
					horizontalOffset.setTo(currentScrollRectangle.x+horizontalOffset.x/2, currentScrollRectangle.y+horizontalOffset.y/2);
					
					DocumentManager.setDocumentScrollPosition(horizontalOffset.x, horizontalOffset.y);
				}
				else {
					ScaleManager.increaseScaleBy(scaleDeltaPercent);
				}
				
			}
			
			public var offsetOnZoomIn:Boolean = false;
			public var centerOnCurrentPoint:Boolean = false;
			public var currentScrollPoint:Point;
			
			protected function button_mouseDownHandler(event:MouseEvent):void {
				if (Radiate.selectedDocument && currentScrollPoint==null && centerOnCurrentPoint) {
					currentScrollPoint = DocumentManager.getCurrentScrollPoint(Radiate.selectedDocument);
				}
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:Scale id="fitToScale"  /> 
	</fx:Declarations>
	
	
	<c:SolidFillButton id="fitButton" 
					   width="30"
					   toolTip="Fit to available screen space"
					   click="fitInButton_clickHandler(event)"
					   label="fit"
					   />
	<c:SolidFillButton id="actualButton" 
					   width="50"
					   toolTip="Show at actual size"
					   click="actualSize_clickHandler(event)"
					   label="100%"
					   fontSize="8"
					   />
	<c:SolidFillButton id="topButton" 
					   width="35"
					   toolTip="Scroll to top and center"
					   click="scrollToTopButton_clickHandler(event)"
					   label="top"
					   includeInLayout="false"
					   visible="false"
					   />
	
	<c:SolidFillButton id="zoomOutButton" 
					   width="30"
					   toolTip="Zoom out"
					   buttonDown="zoomOutButton_buttonDownHandler(event)"
					   mouseUp="button_mouseUpHandler(event)"
					   autoRepeat="true"
					   repeatInterval="100"
					   label="-"/>
	<c:SolidFillButton id="zoomInButton" 
					   width="30"
					   toolTip="Zoom in"
					   buttonDown="zoomInButton_buttonDownHandler(event)"
					   mouseDown="button_mouseDownHandler(event)"
					   mouseUp="button_mouseUpHandler(event)"
					   autoRepeat="true"
					   repeatInterval="100"
					   label="+"/>
	
	<!--
	Pressing ESC key in combox results in the error below when using a custom layout: 
	
	TypeError: Error #1009: Cannot access a property or method of a null object reference. 
	at spark.components.supportClasses::DropDownListBase/http://www.adobe.com/2006/flex/mx/internal::positionIndexInView()
	[E:\dev\4.y\frameworks\projects\spark\src\spark\components\supportClasses\DropDownListBase.as:659]
	
	<s:layout>
		<s:VerticalLayout gap="0" horizontalAlign="contentJustify" requestedRowCount="10" />
	</s:layout>
	-->
	<s:ComboBox id="zoomComboBox" 
				width="58" 
				height="21"
				focusAlpha="0" 
				selectedIndex="8"
				change="zoomComboBox_changeHandler(event)"
				openOnInput="true" 
				arrowKeysWrapFocus="false"
				doubleClickEnabled="true"
				doubleClick="zoomComboBox_doubleClickHandler(event)"
				>
		<s:dataProvider>
			<s:ArrayList>
				<fx:String>3000%</fx:String>
				<fx:String>2800%</fx:String>
				<fx:String>2600%</fx:String>
				<fx:String>2500%</fx:String>
				<fx:String>2400%</fx:String>
				<fx:String>2300%</fx:String>
				<fx:String>2200%</fx:String>
				<fx:String>2175%</fx:String>
				<fx:String>2150%</fx:String>
				<fx:String>2125%</fx:String>
				<fx:String>2100%</fx:String>
				<fx:String>2000%</fx:String>
				<fx:String>1800%</fx:String>
				<fx:String>1600%</fx:String>
				<fx:String>1500%</fx:String>
				<fx:String>1400%</fx:String>
				<fx:String>1300%</fx:String>
				<fx:String>1200%</fx:String>
				<fx:String>1175%</fx:String>
				<fx:String>1150%</fx:String>
				<fx:String>1125%</fx:String>
				<fx:String>1100%</fx:String>
				<fx:String>1000%</fx:String>
				<fx:String>800%</fx:String>
				<fx:String>600%</fx:String>
				<fx:String>500%</fx:String>
				<fx:String>400%</fx:String>
				<fx:String>300%</fx:String>
				<fx:String>200%</fx:String>
				<fx:String>175%</fx:String>
				<fx:String>150%</fx:String>
				<fx:String>125%</fx:String>
				<fx:String>100%</fx:String>
				<fx:String>90%</fx:String>
				<fx:String>80%</fx:String>
				<fx:String>70%</fx:String>
				<fx:String>60%</fx:String>
				<fx:String>50%</fx:String>
				<fx:String>40%</fx:String>
				<fx:String>30%</fx:String>
				<fx:String>20%</fx:String>
				<fx:String>10%</fx:String>
			</s:ArrayList>
		</s:dataProvider>
	</s:ComboBox>
	
</s:HGroup>
