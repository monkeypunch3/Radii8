<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 xmlns:renderers="com.flexcapacitor.views.renderers.*"
		 
		 show="group1_showHandler(event)"
		 creationComplete="group1_showHandler(event)"
		 implements="com.flexcapacitor.views.IInspector" 
		 styleName="inspectablePanel"
		 width="100%">
	
	<fx:Script>
		<![CDATA[
			import com.flexcapacitor.controller.Radiate;
			import com.flexcapacitor.events.RadiateEvent;
			import com.flexcapacitor.model.IDocument;
			import com.flexcapacitor.model.MetaData;
			import com.flexcapacitor.utils.ClassUtils;
			import com.flexcapacitor.utils.DisplayObjectUtils;
			import com.flexcapacitor.utils.supportClasses.ComponentDescription;
			
			import mx.events.FlexEvent;
			
			import org.as3commons.lang.ArrayUtils;
			
			public var radiate:Radiate;
			public var target:Object;
			public var data:Object;
			
			
			public function activate(selectedTarget:Object = null):void {
				radiate = Radiate.getInstance();
				
				radiate.addEventListener(RadiateEvent.TARGET_CHANGE, handleTargetChange, false, 0, true);
				radiate.addEventListener(RadiateEvent.PROPERTY_CHANGED, propertyChangeHandler, false, 0, true);
				radiate.addEventListener(RadiateEvent.HISTORY_CHANGE, propertyChangeHandler, false, 0, true);
				
				if (radiate.target) {
					target = radiate.target;
				}
				
				update();
			}
			
			public function deactivate():void {
				if (radiate) {
					radiate.removeEventListener(RadiateEvent.TARGET_CHANGE, handleTargetChange);
					radiate.removeEventListener(RadiateEvent.PROPERTY_CHANGED, propertyChangeHandler);
					radiate.removeEventListener(RadiateEvent.HISTORY_CHANGE, propertyChangeHandler);
				}
				
				//target = null;
				//data = null;
			}
			
			protected function handleTargetChange(event:RadiateEvent):void {
				if (settingTarget) return;
				if (target!=event.selectedItem) {
					target = event.selectedItem;
					update();
				}
			}
			
			public function update():void {
				var iDocument:IDocument = radiate.selectedDocument;
				var componentDescription:ComponentDescription = iDocument ? iDocument.getItemDescription(target) : null;
				var sourceData:MetaData;
				
				// item was deleted or wrong type
				if (target==null || componentDescription==null) return;
				
				sourceData 					= ClassUtils.getMetaDataOfProperty(target, 	"source");
				sourceRenderer.data 		= sourceData;
				alphaRenderer.data 			= ClassUtils.getMetaDataOfProperty(target, 	"alpha");
				smoothRenderer.data 		= ClassUtils.getMetaDataOfProperty(target, 	"smooth");
				smoothingQualityRenderer.data = ClassUtils.getMetaDataOfStyle(target, 	"smoothingQuality");
				scaleModeRenderer.data 		= ClassUtils.getMetaDataOfProperty(target, 	"scaleMode");
				fillModeRenderer.data 		= ClassUtils.getMetaDataOfProperty(target, 	"fillMode");
				contentLoaderGroupRenderer.data = ClassUtils.getMetaDataOfProperty(target, "contentLoaderGrouping");
				clearOnLoadRenderer.data 	= ClassUtils.getMetaDataOfProperty(target, 	"clearOnLoad");
				
				embedImageData.selected = "bitmapData" in target && target.bitmapData && componentDescription.embedBitmapData;
			}
			
			protected function propertyChangeHandler(event:RadiateEvent):void {
				if (event.type==RadiateEvent.PROPERTY_CHANGED) {
					if (ArrayUtils.containsAny(relevantProperties, event.propertiesAndStyles)) {
						callLater(update);
					}
					else {
						return;
					}
				}
				callLater(update);
			}
			
			protected function group1_showHandler(event:FlexEvent):void {
				var labels:Array = DisplayObjectUtils.getElementsByType(this, Label);
				var hgroups:Array = DisplayObjectUtils.getElementsByType(this, HGroup);
				var length:int = labels.length;
				var gap:int = getStyle("gap");
				var labelWidth:int = getStyle("labelWidth");
				
				for (var i:int;i<length;i++) {
					labels[i].minWidth = labelWidth;
				}
				
				length = hgroups.length;
				
				for (var j:int;j<length;j++) {
					hgroups[j].gap = gap;
				}
			}
			
			public var relevantProperties:Array = ["source", "alpha", "smooth", "smoothingQuality", 
				"scaleMode", "fillMode", "contentLoaderGrouping", "clearOnLoad"];
			
			
			protected function embed_changeHandler(event:Event):void {
				var iDocument:IDocument;
				var componentDescription:ComponentDescription;
				
				iDocument = radiate.selectedDocument;
				componentDescription = iDocument ? iDocument.getItemDescription(target) : null;
				
				if (componentDescription && componentDescription.embedBitmapData != embedImageData.selected) {
					settingTarget = true;
					Radiate.setProperty(componentDescription, "embedBitmapData", embedImageData.selected);
					Radiate.setTarget(target, true, "", true);
					settingTarget = false;
				}
			}
			public var settingTarget:Boolean;
		]]>
	</fx:Script>
	
	<s:layout>
		<s:VerticalLayout />
	</s:layout>
	
	<s:HGroup width="100%" verticalAlign="baseline">
		<s:Label text="Source" styleName="inspectablePanelLabel" />
		<renderers:ImageSourceRenderer id="sourceRenderer" height="24" width="100%"/>
		<s:CheckBox id="embedImageData" toolTip="Embed Image Data" change="embed_changeHandler(event)" baseline="3"/>
	</s:HGroup>
	<s:HGroup width="100%" verticalAlign="baseline">
		<s:Label text="Alpha" styleName="inspectablePanelLabel" />
		<renderers:NumberRenderer id="alphaRenderer" height="24" />
	</s:HGroup>
	<s:HGroup width="100%" verticalAlign="baseline">
		<s:Label text="Smooth" styleName="inspectablePanelLabel" />
		<renderers:BooleanRenderer id="smoothRenderer" height="24" showClearButton="true"/>
	</s:HGroup>
	<s:HGroup width="100%" verticalAlign="baseline">
		<s:Label text="Smooth Quality" styleName="inspectablePanelLabel" />
		<renderers:DropDownListRenderer id="smoothingQualityRenderer" height="24" showClearButton="true"/>
	</s:HGroup>
	<s:HGroup width="100%" verticalAlign="baseline">
		<s:Label text="Scale Mode" styleName="inspectablePanelLabel" />
		<renderers:DropDownListRenderer id="scaleModeRenderer" height="24" defaultValue="stretch"/>
	</s:HGroup>
	<s:HGroup width="100%" verticalAlign="baseline">
		<s:Label text="Fill Mode" styleName="inspectablePanelLabel" />
		<renderers:DropDownListRenderer id="fillModeRenderer" height="24" defaultValue="scale"/>
	</s:HGroup>
	<s:HGroup width="100%" verticalAlign="baseline">
		<s:Label text="Content Grouping" styleName="inspectablePanelLabel" />
		<renderers:TextRenderer id="contentLoaderGroupRenderer" height="24" />
	</s:HGroup>
	<s:HGroup width="100%" verticalAlign="baseline">
		<s:Label text="Clear on Load" styleName="inspectablePanelLabel" />
		<renderers:BooleanRenderer id="clearOnLoadRenderer" height="24" />
	</s:HGroup>
</s:Group>
